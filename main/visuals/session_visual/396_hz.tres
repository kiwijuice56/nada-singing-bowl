[gd_resource type="Shader" format=3 uid="uid://oqgngipnnhw7"]

[resource]
code = "shader_type canvas_item;
// Fractal shader.

// Uses random noise functions on a set of points to produce an image.

uniform vec2 uv_offset;
uniform lowp float time = 0.0;
uniform float aspect_ratio = 1.0;
uniform float color_intensity = 1.0;
uniform float scale = 1.0;
uniform float border = 0.1;
uniform float audio1 = 0.0;

// Noise functions from: 
// https://github.com/tariqksoliman/Fractal-Inferno/blob/master/scripts/FractalInferno.js

vec2 sinusoidal(vec2 c, float offset) {
	c.x = sin(c.x + offset);
	c.y = sin(c.y + offset);
	return c;
}

vec2 spherical(vec2 c, float offset) {
	vec2 t = c;
	c.x = c.x / (c.x * c.x + c.y * c.y) + offset;
	c.y = c.y / (t.x * t.x + t.y * t.y) + offset;
	return c;
}

vec2 julia(lowp vec2 c, lowp vec2 p) {
	int i = 0;
	for (; i < 16; i++) {
		if (c.x * c.x + c.y * c.y > 4.0) {
			break;
		}
		lowp float t = c.x * c.x - c.y * c.y + p.x;
		c.y = 2.0 * c.x * c.y + p.y;
		c.x = t;
	}
	
	lowp float modulus = sqrt(c.x * c.x + c.y * c.y);
	lowp float mu = max(0, (float(i) - log(log(modulus)) / log(2.0))) / 16.0;
	
	return c * vec2(mu);
}

vec2 heart(vec2 c) {
	float r = sqrt(c.x * c.x + c.y * c.y);
	float th = atan(c.y, c.x);
	return vec2(r * sin(th * r), r * -cos(th * r));
}

vec2 swirl(vec2 c) {
	float r = sqrt(c.x * c.x + c.y * c.y);
	return c / (r * r);
}

vec2 disc(vec2 c) {
	float r = sqrt(c.x * c.x + c.y * c.y);
	float th = atan(c.y, c.x);
	return (th / PI) * vec2(sin(PI * r), cos(PI * r));
}

vec2 hyperbolic(vec2 c) {
	float r = sqrt(c.x * c.x + c.y * c.y);
	float th = atan(c.y, c.x);
	return vec2(sin(th) / r, r * cos(th));
}

vec2 cross_f(vec2 c) {
	float s = sqrt(1.0 / pow(pow(c.x, 2) - pow(c.y, 2), 2));
	return c * s;
}

vec2 bubble(vec2 c) {
	float r = 4.0 / (pow(sqrt(pow(c.x, 2) + pow(c.y, 2)), 2) + 4.0);
	return r * c;
}

void fragment() {
	lowp vec2 c = UV;
	
	if (UV.y < border) {
		COLOR.a = c.y / border;
	}
	
	// Setup the coordinate system.
	c += uv_offset; 
	c -= vec2(0.5, 0.5);
	c *= scale;
	c.x *= aspect_ratio;
	
	// Apply functions randomly.
	c = sinusoidal(c, 0.0);
	c = spherical(c, time);
	c = sinusoidal(c, time);
	c = swirl(c);
	c = spherical(c, audio1);
	c = julia(c, c + vec2(time));
	
	// Turn the final coordinate into a color.
	COLOR.rgb = vec3(c.x + c.y + 0.5, 0.05 + c.x * 0.05, 0.1 + 0.3 * c.y) * color_intensity;
}
"
metadata/_edit_res_path = "res://main/visuals/JuliaFractal.tres"
